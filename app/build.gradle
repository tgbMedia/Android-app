apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'org.greenrobot.greendao'

//Load configuration files
def Properties props = new Properties()
def propFile = new File("$project.rootDir/configuration/project.properties")
props.load(new FileInputStream(propFile))

//Stop the build process if some keys is missing
assert propFile.canRead()
assert props.containsKey('TMDB_API_KEY') && props.containsKey('GOOGLE_API_KEY')

repositories {
    maven {
        url 'https://maven.google.com'
    }
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"

    defaultConfig {
        applicationId "com.tgb.media"
        minSdkVersion 17
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        resValue "string", "tmdb_api_key", props['TMDB_API_KEY']
        resValue "string", "google_api_key", props['GOOGLE_API_KEY']

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions("dev2", "prod")

    productFlavors {
        dev {
            dimension "dev2"
            versionNameSuffix "-dev"
            applicationIdSuffix '.dev2'
            minSdkVersion 21
        }

        prod {
            dimension "prod"
        }
    }

    dexOptions {
        preDexLibraries true
        maxProcessCount 8
        javaMaxHeapSize "2048m"
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}



dependencies {
    ext.googleLibrariesVersion = '26.0.0'
    ext.daggerVersion = '2.11'
    ext.butterknifeVersion = '8.8.0'
    ext.greenDaoVersion = '3.2.2'
    ext.glideVersion = '4.0.0'
    ext.rxAndroidVersion = '2.0.1'
    ext.rxJavaVersion = '2.1.2'
    ext.exoPlayerVersion = 'r2.5.0'
    ext.timberVersion = '4.5.1'

    //Logging
    compile "com.jakewharton.timber:timber:$timberVersion"

    //UI Libraries
    ext.shineButtonVersion = '0.1.9'
    ext.coverFlowVersion = 'release-v1.0.4'
    ext.smoothIndicatorVersion = '1.0.1'

    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    //Unit tests
    testCompile 'junit:junit:4.12'

    //Android support libraries
    compile "com.android.support:appcompat-v7:$googleLibrariesVersion"
    compile "com.android.support:design:$googleLibrariesVersion"
    compile "com.android.support:cardview-v7:$googleLibrariesVersion"
    compile "com.android.support:mediarouter-v7:$googleLibrariesVersion"
    compile "com.android.support:support-core-utils:$googleLibrariesVersion"
    compile "com.android.support:percent:$googleLibrariesVersion"
    compile "com.android.support:leanback-v17:$googleLibrariesVersion"
    compile 'com.android.support.constraint:constraint-layout:1.1.0-beta1'
    compile 'me.henrytao:smooth-app-bar-layout:25.2.0.0' //Deprecated!

    //GreenDao
    compile "org.greenrobot:greendao:$greenDaoVersion"

    //Dagger
    compile "com.google.dagger:dagger:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"

    //Butter knife
    compile "com.jakewharton:butterknife:$butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    //RxJava
    compile "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    compile "io.reactivex.rxjava2:rxjava:$rxJavaVersion"

    //Glide
    compile "com.github.bumptech.glide:glide:$glideVersion"

    //ExoPlayer
    compile "com.google.android.exoplayer:exoplayer:$exoPlayerVersion"

    //YouTube
    compile files('libs/YouTubeAndroidPlayerApi.jar')

    //GUI
    compile "com.sackcentury:shinebutton:$shineButtonVersion"
    compile "com.github.crosswall:Android-Coverflow:$coverFlowVersion"
    compile "com.merhold.extensiblepageindicator:extensiblepageindicator:$smoothIndicatorVersion"

    //TGB Server(client PC) Api
    compile project(':TGB Media server API')

    //Other TGB Media libraries
    compile project(':tmdb')
}
